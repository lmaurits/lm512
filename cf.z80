; Register definitions
.DATA:		equ CFBASE + 0x00	; Data (R/W)
.ERR:		equ CFBASE + 0x01	; Error register (R)
.FEAT:		equ CFBASE + 0x01	; Features (W)
.SECCO:		equ CFBASE + 0x02	; Sector count (R/W)
.SECNO:		equ CFBASE + 0x03	; Sector number (R/W)
.LBA0:		equ CFBASE + 0x04	; LBA bits 0-7 (R/W)
.CYLLO:		equ CFBASE + 0x04	; Cylinder low (R/W)
.LBA1:		equ CFBASE + 0x05	; LBA bits 8-15 (R/W)
.CYLHI:		equ CFBASE + 0x05	; Cylinder high (R/W)
.LBA2:		equ CFBASE + 0x06	; LBA bits 16-23 (R/W)
.DRHD:		equ CFBASE + 0x06	; Drive/head (R/W)
.LBA3:		equ CFBASE + 0x06	; LBA bits 24-27 (R/W)
.STAT:		equ CFBASE + 0x07	; Status (R)
.CMD:		equ CFBASE + 0x07	; Command (W)

CfSetup:
	; Activate 8 bit data transfer mode
	call		WaitBusy
	ld		a, $01		; Set features register to enable 8 bit
	out		(.FEAT), a
	call		WaitBusy
	ld		a, $EF		; Send set features command
	out		(.CMD), a
	call		WaitBusy
	ret
SetLBA:
	; Set up LBA address for start of disk
	; Addresses 0, 1, 2 are in regs A, B, D
	push		af
	call		WaitBusy
	pop		af
	ld		a, 0
	out		(.LBA0), a	; Set LBA addresses 0
	call		WaitBusy
	ld		a, 0		; Set LBA addresses 1
	out		(.LBA1), a
	call		WaitBusy
	ld		a, 0		; Set LBA addresses 2
	out		(.LBA2), a
	call		WaitBusy
	ld		a, 224		; Set LBA 3 to 0, plus flags
	out		(.LBA3), a
	call		WaitBusy
	ret
SetSectorCount:
	push		af
	call		 WaitBusy
	pop		af
	out		(.SECCO), a
	ret
SendReadCommand:
	call		WaitBusyReady	; Make sure drive is ready for command
	ld		a, $20		; Prepare read command
	out		(.CMD), a		; Send read command
	call		WaitBusyData	; Wait until data is ready to be read
	in		a, (.STAT)		; Read status
	and		%00000001		; mask off error bit
	jp		nz, SendReadCommand	; Try again if error
	ret
WaitBusy:
	in		a, (.STAT)		; Read status
	and		%10000000		;mask off busy bit
	jr		nz, WaitBusy		;we want busy(7) to be 0
	ret

WaitBusyReady:
	in		a, (.STAT)		; Read status
	and		%11000000		;mask off busy and rdy bits
	xor		%01000000		;we want busy(7) to be 0 and rdy(6) to be 1
	jr		nz, WaitBusyReady
	ret

WaitBusyData:
	in		a, (.STAT)		; Read status
	and		%10001000		;mask off busy and drq bits
	xor		%00001000		;we want busy(7) to be 0 and drq(3) to be 1
	jr		nz, WaitBusyData
	ret
