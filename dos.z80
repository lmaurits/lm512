;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; DISK OPERATING SYSTEM
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

include 'fat.z80'

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; DOS CORE
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Dos:
DosInit:
	; INITIALISE EVERYTHING
	
	; Print welcome message
	ld hl, welcomestr
	call PrintStringA
	; Initialise system variables
	ld		hl, 0x8000
	ld		(execaddr), hl
	ld		hl, 0x8000
	ld		(fileaddr), hl
	ld		hl, 0xFFFF
	ld		(prgstack), hl
	; Setup UART buffering
	ld		a, 0x00
	ld		(uart_buffering), a
	ld		(uart_buffer_size), a
	ld		hl, uart_buffer
	ld		(uart_buffer_pointer), hl
	; Initialise filesystem
	call		InitFAT
	; Check for AUTOEXEC.BAT
	call		DosAutoExec
	; Start processing commands
	jp DosLoop
	welcomestr: defm "Welcome to LM-512 microcomputer system.\n\r\0"

DosAutoExec:
	; CHECK FOR FILE NAMED AUTOEXEC.BAT
	; IF FOUND, EXECUTE AS BATCH FILE

	ld		hl, DosAutoExecStr
	ld		(target_filename_ptr), hl
	call		FindFile
	ret		nz
	; Dent *should* now be pointed at our target
	jp		BatchCallback
DosAutoExecStr:
	defm		"AUTOEXEC.BAT\0"

DosLoop:
	; INFINITE LOOP OF READING A COMMAND LINE,
	; PARSING IT AND DISPATCHING THE COMMAND

	ld		hl, dos_promptstr
	call		PrintStringA
	ld		hl, dos_linebuf
	call		ReadLineA
	jr		nz, DosLoop ; User stopped input using Ctrl-C
	ld		hl, dos_linebuf
	ld		a, (hl)
	cp		"\0"
	jr		z, DosLoop
	call		DosHandleLine
	jr		DosLoop
dos_promptstr:
	defm		"> \0"

DosHandleLine:
	; HANDLE A COMMAND LINE: CONVERT TO UPPERCASE,
	; PARSE/TOKENISE LINE AND DISPATCH
	ld		bc, dos_argc
	ld		de, dos_dispatch_table
	call		HandleCommandLine
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; COMMAND DISPATCH
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Command strings

batchstr:	defm "BATCH\0"
clsstr:		defm "CLS\0"
cpstr:		defm "CP\0"
datestr:	defm "DATE\0"
echostr:	defm "ECHO\0"
execstr:	defm "EXEC\0"
flashstr:	defm "FLASH\0"
lsstr:		defm "LS\0"
monstr:		defm "MONITOR\0"
mvstr:		defm "MV\0"
serloadstr:	defm "SERLOAD\0"
receivestr:	defm "RECEIVE\0"
resetstr:	defm "RESET\0"
resumestr:	defm "RESUME\0"
rmstr:		defm "RM\0"
loadstr:	defm "LOAD\0"
setstr:		defm "SET\0"
showstr:	defm "SHOW\0"
sleepstr:	defm "SLEEP\0"
typestr:	defm "TYPE\0"
uptimestr:	defm "UPTIME\0"

; Table linking command strings to function entry points
dos_dispatch_table:
	defw batchstr, Batch
	defw clsstr, Cls
	defw cpstr, Cp
	defw datestr, Date
	defw execstr, Exec
	defw echostr, Echo
	defw flashstr, Flash
	defw lsstr, Ls
	defw helpstr, DosHelp
	defw loadstr, DosLoadFile
	defw monstr, EnterMonitor
	defw mvstr, Mv
	defw receivestr, Receive
	defw resetstr, Reset
	defw resumestr, Resume
	defw rmstr, Rm
	defw serloadstr, SerialLoad
	defw setstr, Set
	defw showstr, Show
	defw sleepstr, SleepDos
	defw typestr, Type
	defw uptimestr, Uptime
	defw nullstr, Default

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Buffered UART
ReadChar:
	push		hl
ReadCharReenter:
	di
	ld		a, (uart_buffer_size)
	cp		0
	jr		z, ReadCharWait
	ld		hl, (uart_buffer_pointer)
	dec		hl
	ld		a, (hl)
	ld		(uart_buffer_pointer), hl
	ld		hl, uart_buffer_size
	dec		(hl)
	ei
	pop		hl
	ret
ReadCharWait:
	ei
	halt
	jr		ReadCharReenter

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; DOS COMMANDS
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Batch:
	; Load a file's contents into RAM and
	; execute it line-by-line as if it were
	; input to the shell

	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, BatchValidate
	;; Argc error
	ld		hl, batchstr
OneFilenameArgcError:
	call		PrintStringA
	ld		hl, ExpectsExactlyOneArgStr
	call		PrintStringA
	ld		hl, FilenameStr
	call		PrintStringA
	ret
BatchValidate:
	call		ValidateOneExistingFile
	ret		nz
BatchCallback:
	; At this point, we've matched
	; Read the file into memory
	ld		hl, (fileaddr)
	call		ReadFile
	ld		hl, (fileaddr)
	ld		(line_start), hl
	ld		bc, (size_in_bytes)
BatchLoop:
	ld		a, 0x0A		; Newline
	cpir
	ld		(line_end), hl
	push		bc
	; Overwrite newline with string terminator
	dec		hl
	ld		(hl), "\0"
	ld		hl, (line_start)
	ld		bc, dos_argc
	ld		de, dos_dispatch_table
	call		DosHandleLine
	ld		hl, (line_end)
	ld		(line_start), hl
	pop		bc
	ld		a, b
	or		c
	ret		z
	jr		BatchLoop

ValidateOneExistingFile:
	ld		hl, (dos_argv)
	call		ValidateFilename
	ret		nz
	ld		(target_filename_ptr), hl
	call		FindFile
	ret		z
	call		BadFileCallback
	or		1
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Date:
	ld		a, (dos_argc)
	cp		0
	jp		z, DateGet
	cp		1
	jr		z, DateSet
	ld		hl, DateArgcErrStr
	call		PrintStringA
	ret
DateArgcErrStr:
	defm		"Date expects zero (to get) or one arguments (to set, YYMMDDHHmmSS)\r\n\0"
DateSet:
	ld		hl, (dos_argv)
	call		ValidateDateString
	ret		nz
	call		SetTimeFromString
ValidateDateString:
	call		StrLen
	cp		12
	jr		nz, ValidateDateStringErr
	call		IsStrDec
	jr		nz, ValidateDateStringErr
ValidateDateStringOkay:
	cp		a	; Set zero flag
	ret
ValidateDateStringErr:
	ld		hl, ValidateDateStringErrStr
	call		PrintStringA
	or		1	; Reset zero flag
	ret
ValidateDateStringErrStr:
	defm		"Set date with YYMMDDHHmmSS format\r\n\0"
SetTimeFromString:
	call		ReadBCDByte
	res		7, a			; Strip 7th bit
	push		af
	call		ReadBCDByte
	res		7, a
	push		af
	call		ReadBCDByte
	res		7, a
	ld		e, a
	call		ReadBCDByte
	res		7, a
	ld		d, a
	call		ReadBCDByte
	res		7, a
	ld		c, a
	call		ReadBCDByte
	res		7, a
	ld		b, a
	pop		af
	ld		h, a
	pop		af
	ld		l, a
	call		RTC_WRITE
	ret
DateGet:
	ld hl, 		message_buffer
	call		BuildTimeStr
	ld hl,		message_buffer
	call 		PrintStringA
	ret
BuildTimeStr:
	push		hl		; Save string pointer
	call		RTC_READ
	ex		de, hl		; Move year and month to DE
	pop		hl		; Restore string pointer

	; Year
	ld		(hl), "2"
	inc hl
	ld		(hl), "0"
	inc hl
	ld		a, e
	call WriteComponent
	ld		(hl), " "
	inc hl
	ld		a, d
	; Turn month into non-BCD
	ld		b, a
	; Mask off 10 digits in A
	and		00001111b
	srl 		b	; Shift down 10 digi
	srl		b
	srl		b
	srl		b
TimeStrLoop:
	add		10
	djnz		TimeStrLoop
	; Now deduct 1 (so Jan is offset 0)
	sub		1
	; Now multiply A by 3 (length of a month str)
	ld		b, a
	add		b
	add		b
	; Now use this as an index into month strings
	ld		b, 0
	ld		c, a
	ex		de, hl
	ld		hl, MonthStrings
	add		hl, bc
	ld		bc, 3
	ldir
	ex		de, hl
	ld (hl), " "
	inc hl
	; Day

	push		hl		; Save string pointer
	call		RTC_READ
	pop		hl		; Restore string pointer
	push		bc
	ld		a, e
	call WriteComponent
	ld (hl), " "
	inc hl

	ld		a, d
	call WriteComponent
	ld (hl), ":"
	inc hl
	ld		a, c
	call WriteComponent
	ld (hl), ":"
	inc hl
	pop 		bc
	ld		a, b
	call WriteComponent
	call TerminateLine
	ret

WriteComponent:
	ld b, a			; Copy
	srl a			; Shift down 10 digi
	srl a
	srl a
	srl a
	and 00000111b		; Mask off AM/PM
	add a, 48			; Convert to  ASCII
	ld (hl), a
	inc hl
	ld a, b
	and 00001111b
	add a, 48	; ASCII
	ld (hl), a
	inc hl
	ret

MonthStrings:
	defm	"JanFebMarAprMayJunJulAugSepOctNovDec"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; ECHO
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Echo:
	ld		a, (dos_argc)
	ld		b, a
	ld		hl, dos_argv
EchoLoop:
	push		bc
	ld		e, (hl)
	inc		hl
	ld		d, (hl)
	inc		hl
	ex		de, hl
	call		PrintStringA
	ld		a, " "
	call		SERIAL_WRITE_A
	ex		de, hl
	pop		bc
	djnz		EchoLoop
	ld		a, "\r"
	call		SERIAL_WRITE_A
	ld		a, "\n"
	call		SERIAL_WRITE_A
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Flash:
	ld		a, (dos_argc)
	cp		1
	jr		z, FlashValidate
	;; Argc error
	ld		hl, flashstr
	call		OneFilenameArgcError
	ret
FlashValidate:
	call		ValidateOneExistingFile
	ret		nz
	;; Check file is not too large
	ld		hl, (size_in_bytes+2)	; upper 16 bit
	ld		a, h
	or		l
	cp		0
	jr		nz, FlashTooLarge
	ld		bc, (size_in_bytes)	; lower 16 bit
	ld		hl, 0x2000
	or	a			; Clear carry flag
	sbc		hl, bc
	jr		c, FlashTooLarge
	;; First, warn the user
	ld		hl, FlashNotice
	call		PrintStringA
	;; Now copy the core of this function into RAM
	;; where it will not get clobbered
	ld		hl, FlashRelocatablePayload
	ld		de, 0x8000	; Ignore FILEADDR, this is terminal!
	ld		bc, FlashRelocatablePayloadEnd - FlashRelocatablePayload	; Size of payload
	ldir			; Relocate
	jp		0x8000		; Do it!
FlashNotice:
	defm	"Rewriting firmware.  Good luck!\r\n\0"
FlashTooLarge:
	ld		hl, FlashTooLargeStr
	call		PrintStringA
	ret
FlashTooLargeStr:
	defm	"ROM image too large!\r\n\0"

FlashRelocatablePayload:
	; NB: This is loaded into RAM at address 0x9000
	; But the assembler doesn't know this
	; So ONLY use jr, not jp, below
	ld		hl, 0x9000
	ld		(fileaddr), hl
	call		DosLoadFile	; Get new file in mem
	; Set BC to byte count
	ld		bc, (size_in_bytes)
	; Set HL to location of new file
	ld		hl, 0x9000
	; Set DE to destination
	ld		de, $0000	; DEST in ROM is $0000
	; Disable interrupts!
	di
FlashLoop:
	ld		b, (hl)		; Get the new byte in B
	ld		a, (de)		; Get the current byte in A
	cp		b		; Compare 'em
	jr		z, delay_loop_end; Don't overwrite needlessly
	ld		a, b
	ld		(de), a
	;;; Hacky delay loop
	exx
	ld		de, $071C
delay_loop:
	nop
	dec		de
	ld		a, d
	or		e
	jr		nz, delay_loop
	exx
delay_loop_end:
	;;; Back to copying...
	inc		hl
	inc		de
	dec		bc
	ld		a, b
	or		c
	jr		nz, FlashLoop
	; Reset when done
	rst		0x00
FlashRelocatablePayloadEnd:

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Ls:
	ld	hl,0x0000
	ld	(file_count), hl
	ld	(total_size), hl
	ld	(total_size+2), hl

	call	ResetRootDir		; Jump to start of root dir
LsLoop:
	call	TestEndDir
	jr	z, LsEnd
	call	TestFreeEntry
	jr	z, LsNext
	call	TestLFN
	jr	z, LsNext
	call	LsHandleFile
LsNext:
	call	AdvanceDirEnt
	jr	LsLoop
LsEnd:
	ld	hl, TotalStr
	call	PrintStringA
	ld	hl, (file_count)
	call	B2D16
	ld	hl, B2DBUF
	call	PrintStringA
	ld	hl, FilesStr
	call	PrintStringA

	ld	hl, BytesLeadStr
	call	PrintStringA
	ld	hl, (total_size)
	ld	de, (total_size+2)
	call	B2D32
	ld	hl, B2DBUF
	call	PrintStringA
	ld	hl, BytesStr
	call	PrintStringA
	ret

LsHandleFile:
	ld	hl, (file_count)
	inc	hl
	ld	(file_count), hl
	ld	hl, (total_size)
	ld	de, (size_in_bytes)
	add	hl, de
	ld	(total_size), hl
	ld	hl, (total_size+2)
	ld	de, 0
	adc	hl, de
	ld	(total_size+2), hl
	ld	hl, filename_buffer
	ld	a, " "
	ld	b, 12
LsBlankingLoop:
	ld	(hl), a
	inc	hl
	djnz	LsBlankingLoop
	call	BuildFilenameString
	ld	hl, filename_buffer
	ld	de, message_buffer
	ld	bc, 12
	ldir
	; Ugly, ugly clean up
	ld	de, message_buffer
	ld	b, 12
LsCleanLoop:
	ld	a, (de)
	cp	"\0"
	jr	z, LsCleanFix
	inc	de
	djnz	LsCleanLoop
	jr	LsUgh
LsCleanFix:
	ld	a, " "
	ld	(de), a
	jr	LsCleanLoop
LsUgh:
	push	de
	ld	hl, (size_in_bytes)
	ld	de, (size_in_bytes+2)
	call	B2D32
	pop	hl
	ld	a, 24
	sub	c
	push	bc
	ld	b, a
LsSpaceLoop:
	ld	(hl), " "
	inc	hl
	djnz	LsSpaceLoop
	pop	bc
	call	AppendB2D
	ld	(hl), "\0"
	ld	hl, message_buffer
	call	PrintStringA
	ld	hl, BytesStr
	call	PrintStringA
	ret
TotalStr:
	defm	"\nTotal:\t\t\t\0"
FilesStr:
	defm	" files\r\n\0"
BytesLeadStr:
	defm	"\t\t\t\0"
BytesStr:
	defm	" bytes\r\n\0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

DosHelp:
	ld hl, dos_helpmessage
	call PrintStringA
	ret
	dos_helpmessage:	defm "LM-512 DOS commands:\n\r"
	defm "BATCH\t\tExecute commands from text file\n\r"
	defm "CLS\t\tClear screen\n\r"
	defm "CP\t\tCopy file\n\r"
	defm "DATE\t\tPrint/set date and time\n\r"
	defm "ECHO\t\tEcho string\n\r"
	defm "EXEC\t\tLoad and execute machine code from file\n\r"
	defm "FLASH\t\tFlash ROM from file\n\r"
	defm "LOAD\t\tLoad file onto memory\n\r"
	defm "LS\t\tList directory contents\n\r"
	defm "MONITOR\t\tEnter resident monitore\n\r"
	defm "MV\t\tMove (rename) file\n\r"
	defm "RECEIVE\t\tRead data from COM2 and save to file\n\r"
	defm "RESET\t\tReset system\n\r"
	defm "RM\t\tremove file\n\r"
	defm "SERLOAD\t\tRead data from COM2 to memory\n\r"
	defm "SET\t\tSet system var\n\r"
	defm "SHOW\t\tShow system var values\n\r"
	defm "SLEEP\t\tSleep for some seconds\n\r"
	defm "TYPE\t\tShow file contents\n\r"
	defm "UPTIME\t\tShow time since boot\n\r"
	defm "\0"
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

EnterMonitor:
	call		Monitor
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

proto_hello: equ $00
proto_hello_ack: equ $0F
proto_bytes: equ $F0
proto_send:  equ $FF

SerialLoad:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, SerialLoad00
	;; Prepare and pring arg error
	ld		hl, serloadstr
	call		PrintStringA
	ld		hl, ExpectsExactlyOneArgStr
	call		PrintStringA
	ld		hl, AddressStr
	call		PrintStringA
	ret
ExpectsExactlyOneArgStr:
	defm		" expects exactly one arg \0"
AddressStr:
	defm		"(mem address)\r\n\0"
SerialLoad00:
	; Validate address
	ld		hl, (dos_argv)
	call		ValidateAddress
	ret		nz
	; Parse address and store in HL
	call		Read16bit
	ld		h, b
	ld		l, c
SerialLoadCore:
	; Track how many bytes we receive
	ld		de, 0x0000
	; Reset server
	ld		a, proto_hello
	call		SERIAL_WRITE_B
	call		SERIAL_READ_B
	cp		proto_hello_ack
	jr		z, SerialLoad01
SerialLoad01:
transfer_outer_loop:
	ld		a, proto_bytes
	call		SERIAL_WRITE_B
	call		SERIAL_READ_B
	; Make sure we wren't just told there are no more bytes
	cp		0
	jr		z, SerialLoadEnd
	; A now contains byte count.  Turn to 16 bit in BC
	ld		b, 0
	ld		c, a
	; Increment byte received count
	ex		de, hl
	add		hl, bc
	ex		de, hl
	ld		a, proto_send
	call		SERIAL_WRITE_B
transfer_inner_loop:
	call		SERIAL_READ_B
	ld		(hl), a
	inc		hl
	dec		c
	ld		a, c
	cp		0
	jr		nz, transfer_inner_loop
	ld		a, "."
	call		SERIAL_WRITE_A
	jr		transfer_outer_loop
SerialLoadEnd:
	ex		de,hl		; Byte count in hl
	call		B2D16		; Decimalise
	call		PrintStringA
	ld		hl, SerialLoadStr
	call		PrintStringA
	ret
SerialLoadStr:
	defm		" bytes transferred.\r\n\0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; SET
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; This could be made smaller by sharing code with cli/Dispatch
Set:
	ld		a, (dos_argc)
	cp		2
	jr		z, SetValidate
	;; Argc error
	ld		hl, setstr
	call		PrintStringA
	ld		hl, ExpectsExactlyTwoArgsStr
	call		PrintStringA
	ld		hl, SetArgsStr
	call		PrintStringA
	ret
SetValidate:
	ld		hl, (dos_argv)
	ld		bc, SetTable
	call		MapString2Address
	; Now HL is either a RAM address or 0x0000
	ld		a, l
	cp		0
	jr		nz, SetDoIt
	ld		a, h
	cp		0
	jr		nz, SetDoIt
	; At this point we know the address is 0000, i.e. we did
	; not really match
SetError:
	ld		hl, SetBadVarStr
	call		PrintStringA
	ret
SetDoIt:
	push		hl	; The address
	ld		hl, (dos_argv+2)
	call		Read16bit
	pop		hl
	ld		(hl), c
	inc		hl
	ld		(hl), b
	ret

ExpectsExactlyTwoArgsStr:
	defm		" expects exactly two args \0"
SetArgsStr:
	defm		"(one variable name, one value).\r\n\0"
SetBadVarStr:
	defm		"Unrecognised variable.  Run SHOW to see all vars.\r\n\0"

execaddrstr:
	defm		"EXECADDR\0"
fileaddrstr:
	defm		"FILEADDR\0"
prgstackstr:
	defm		"PRGSTACK\0"
int1str:defm		"INT1\0"
int2str:defm		"INT2\0"
int3str:defm		"INT3\0"
int4str:defm		"INT4\0"
int5str:defm		"INT5\0"
int6str:defm		"INT6\0"
int7str:defm		"INT7\0"
int8str:defm		"INT8\0"

SetTable:
	defw execaddrstr, execaddr
	defw fileaddrstr, fileaddr
	defw prgstackstr, prgstack
	defw int1str, int_vector
	defw int2str, int_vector+2
	defw int3str, int_vector+4
	defw int4str, int_vector+6
	defw int5str, int_vector+8
	defw int6str, int_vector+10
	defw int7str, int_vector+12
	defw int8str, int_vector+14
	defw nullstr, 0x0000

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; SHOW
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Show:
	ld		de, SetTable
ShowLoop:
	ex		de, hl
	ld		e, (hl)
	inc		hl
	ld		d, (hl)
	inc		hl
	ex		de, hl
	call		StrLen
	cp		0
	ret		z
	call		PrintStringA
	ld		hl, SeparatorStr
	call		PrintStringA
	ex		de, hl
	ld		c, (hl)
	inc		hl
	ld		b, (hl)
	inc		hl
	ex		de, hl
	ld		hl, message_buffer
	inc		bc
	ld		a, (bc)
	push		bc
	call		StrfHex
	pop		bc
	dec		bc
	ld		a, (bc)
	call		StrfHex
	call		TerminateLine
	ld		hl, message_buffer
	call		PrintStringA
	jr		ShowLoop
SeparatorStr:
	defm		":\t\0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; RECEIVE FILE
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Receive:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, ReceiveValidate
	;; Argc error
	ld		hl, receivestr
	jp		OneFilenameArgcError
FilenameStr:
	defm		"(8.3 filename(s))\r\n\0"
ReceiveValidate:
	ld		hl, (dos_argv)
	call		ValidateFilename
	ret		nz
	call		HandleOverwrite
	ret		nz
	;; First download the file
	ld		hl, (fileaddr)
	call		SerialLoadCore
	;; Now save
	ld		hl, (fileaddr)
	push		de
	pop		bc
	ld		de, (dos_argv)
	call		WriteFile
	ret

HandleOverwrite:
	;; Check for collisions!
	call		FindFile
	jr		z, HandleOverwriteCore
	cp		a
	ret
HandleOverwriteCore:
	ld		hl, FileExistsStr
	call		PrintStringA
	jr		HandleOverwriteAskQuickEnter
HandleOverwriteAskLoop:
	ld		hl, YNPromptStr
	call		PrintStringA
HandleOverwriteAskQuickEnter:
	ld		hl, message_buffer
	call		SERIAL_READ_A
	ld		(hl), a
	inc		hl
	call		TerminateLine
	ld		hl, message_buffer
	call		ConvertToUpper
	call		PrintStringA
	ld		hl, message_buffer
	ld		a, (hl)
	cp		"Y"
	jr		z, HandleOverwriteApprove
	cp		"N"
	jr		z, HandleOverwriteDeny
	jr		HandleOverwriteAskLoop
HandleOverwriteApprove:
	call		DeleteFile
	cp		a ; Set zero flag
	ret
HandleOverwriteDeny:
	or		1 ; Clear zero flag
	ret
FileExistsStr:
	defm		"File exists!  Overwrite? (Y/N) \0"
YNPromptStr:
	defm		"Enter Y or N: \0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; RESET
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Reset:
	rst		0x00

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; RENAME FILE (MV)
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Mv:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		2
	jr		z, Mv00
	;; Argc error
	ld		hl, mvstr
TwoFilenamesArgcError:
	call		PrintStringA
	ld		hl, ExpectsExactlyTwoArgsStr
	call		PrintStringA
	ld		hl, FilenameStr
	call		PrintStringA
	ret
Mv00:
	call		ValidateTwoFilesFirstExistsHandleOverwrite
	ret		nz
MvCore:
	ld		hl, (dos_argv)
	ld		de, (dos_argv+2)
	call		RenameFile
	ret
BadFileCallback:
	ld	hl, BadFileMsg
	call	PrintStringA
	ret
BadFileMsg:
	defm	"Bad command or filename.\r\n\0"

ValidateTwoFilesFirstExistsHandleOverwrite:
	call		ValidateOneExistingFile
	ret		nz
	ld		hl, (dos_argv+2)
	call		ValidateFilename
	ret		nz
	call		HandleOverwrite
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; REMOVE FILE (RM)
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Rm:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, RmValidate
	;; Argc error
	ld		hl, rmstr
	jp		OneFilenameArgcError
RmValidate:
	call		ValidateOneExistingFile
	ret		nz
	; Dent *should* now be pointed at our target
	call		DeleteFile
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; CLEAR SCREEN (CLS)
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Cls:
	ld	hl, clsstring
	call	PrintStringA
	ret
clsstring:
	defb	0x1b
	defm	"[2J"
	defb	0x1b
	defm	"[H\0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; COPY FILE (CP)
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Cp:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		2
	jr		z, Cp00
	;; Argc error
	ld		hl, cpstr
	jp		TwoFilenamesArgcError
Cp00:
	call		ValidateTwoFilesFirstExistsHandleOverwrite
	ret		nz
	ld		hl, (dos_argv)
	call		FindFile
	call		LoadFileToFileAddr
	ld		hl, (fileaddr)
	ld		de, (dos_argv+2)
	ld		bc, (size_in_bytes)
	call		WriteFile
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; LOAD FILE
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

DosLoadFile:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, LoadValidate
	cp		2
	jr		z, LoadValidate
	;; Argc error
	ld		hl, loadstr
OneFilenameOptionalAddressArgcError:
	call		PrintStringA
	ld		hl, ExpectsOneOrTwoArgStr
	call		PrintStringA
	ld		hl, FilenameAddrStr
	call		PrintStringA
	ret
ExpectsOneOrTwoArgStr:
	defm		" expects one or two args \0"
FilenameAddrStr:
	defm		"(8.3 filename and optional memory address)\r\n\0"
LoadValidate:
	call		ValidateOneExistingFile
	ret		nz
	; At this point, we know the file exists
	; and the current dent is pointing at it
	; If we only got the one argument,
	; load file to location specified by
	; FILEADDR sys var
	ld		a, (dos_argc)
	cp		1
	jr		z, LoadFileToFileAddr
	; If we got a second arg, treat it as the
	; loading address
	ld		hl, (dos_argv+2)
	call		ValidateAddress
	ret		nz
	; Parse address and store on stack
	call		Read16bit
	ld		h, b
	ld		l, c
	jr		LoadFile
LoadFileToFileAddr:
	ld		hl, (fileaddr)
	; Dent *should* now be pointed at our target
	; Read the file into memory
LoadFile:
	call		ReadFile
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Default Handler
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Default:
	ld		hl, dos_linebuf
	ld		(target_filename_ptr), hl
	call		FindFile
	jp		nz, BadFileCallback
	; At this point, we know the file exists
	; Is this a batch file?
	ld		hl, extension
	ld		de, BatchExtensionStr
	call		StrCmp
	; If so, treat it as such
	jp		z, BatchCallback
	; If not, treat it as Z80 machine code
	jr		ExecFileAtExecAddr
BatchExtensionStr:
	defm		"BAT\0"

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; EXEC FILE
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Exec:
	; Check dos_argc
	ld		a, (dos_argc)
	cp		1
	jr		z, ExecValidate
	cp		2
	jr		z, ExecValidate
	;; Argc error
	ld		hl, execstr
	jp		OneFilenameOptionalAddressArgcError
ExecValidate:
	call		ValidateOneExistingFile
	ret		nz
	; If we only got the one argument,
	; load file to location specified by
	; FILEADDR sys var
	ld		a, (dos_argc)
	cp		1
	jr		z, ExecFileAtExecAddr
	; If we got a second arg, treat it as the
	; loading address
	ld		hl, (dos_argv+2)
	call		ValidateAddress
	ret		nz
	; Parse address and store on stack
	call		Read16bit
	ld		h, b
	ld		l, c
	jr		ExecCore
ExecFileAtExecAddr:
	ld		hl, (execaddr)
	; Dent *should* now be pointed at our target
	; Read the file into memory
ExecCore:
	call		ReadFile
	; Setup stack
	ld		(dos_stack_backup), sp
	ld		hl, (prgstack)
	ld		sp, hl
	; Turn on UART buffering
	ld		a, 0xFF
	ld		(uart_buffering), a
	; LED on!
	ld		a, 0x01
	call		Notify
	; Can't actually call hl or (hl), so do this manually
	; with a push and jump
	ld	hl, ExecReturnAddress
	push	hl
	ld	hl, (execaddr)
	jp	(hl)
ExecSuspendAddress:
	push		af
	push		bc
	push		de
	push		hl
	ex		af, af'
	exx
	push		af
	push		bc
	push		de
	push		hl
	ex		af, af'
	exx
	push		ix
	push		iy
	ld		(dos_resume_stack_ptr), sp
ExecReturnAddress:
	; Turn off UART buffering
	ld		a, 0x00
	ld		(uart_buffering), a
	; Led off!
	ld		a, 0x00
	call		Notify
	; Restore stack
	ld		sp, (dos_stack_backup)
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; RESUME
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Resume:
	ld		(dos_stack_backup), sp
	ld		sp, (dos_resume_stack_ptr)
	ld		a, 0xFF
	ld		(uart_buffering), a
	pop		iy
	pop		ix
	ex		af, af'
	exx
	pop		hl
	pop		de
	pop		bc
	pop		af
	ex		af, af'
	exx
	pop		hl
	pop		de
	pop		bc
	pop		af
	ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; TYPE
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

Type:
	ld		a, (dos_argc)
	cp		1
	jr		z, TypeValidate
	;; Argc error
	ld		hl, typestr
	jp		OneFilenameArgcError
TypeValidate:
	call		ValidateOneExistingFile
	ret		nz
	; Dent *should* now be pointed at our target
	ld	hl, (fileaddr)
	call	ReadFile
	ld	hl, (fileaddr)
	ld	de, (size_in_bytes)
TypeLoop:
	ld	a, (hl)
	cp	0x0A	; newline
	jr	z, TypeNewline
	call	SERIAL_WRITE_A
TypeIncrement:
	inc	hl
	dec	de
	ld	a, d
	or	e
	jr	nz, TypeLoop
	ret
TypeNewline:
	ld	a, "\r"
	call	SERIAL_WRITE_A
	ld	a, "\n"
	call	SERIAL_WRITE_A
	jr	TypeIncrement

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; TIMING STUFF
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

SleepDos:
	ld		a, (dos_argc)
	cp		1
	jr		z, SleepDos00
	;; Argc error
	ld		hl, sleepstr
	call		PrintStringA
	ld		hl, ExpectsExactlyOneArgStr
	call		PrintStringA
	ld		hl, SleepArgsStr
	call		PrintStringA
	ret
SleepArgsStr:
	defm		"(2 digit hex second count)\r\n\0"
SleepDos00:
	; Validate address
	ld		hl, (dos_argv)
	call		ValidateValue
	ret		nz
	; Parse address and store in HL
	call		Read8bit
	call		Sleep
	ret

Uptime:
	ld		hl, up_string
	call		PrintStringA
	ld		hl, (uptime_days)
	call		B2D16
	ld		hl, message_buffer
	call		AppendB2D
	ld		(hl), "\0"
	ld		hl, message_buffer
	call		PrintStringA
	ld		hl, days_string
	call		PrintStringA
	ld		a, (uptime_hours)
	call		B2D8
	call		PadTime
	ld		hl, message_buffer
	call		AppendB2D
	ld		(hl), ":"
	inc		hl
	push		hl
	ld		a, (uptime_mins)
	call		B2D8
	call		PadTime
	pop		hl
	call		AppendB2D
	ld		(hl), ":"
	inc		hl
	push		hl
	ld		a, (uptime_secs)
	call		B2D8
	call		PadTime
	pop		hl
	call		AppendB2D
	call		TerminateLine
	ld		hl, message_buffer
	call		PrintStringA
	ret
up_string:
	defm		"Up \0"
days_string:
	defm		" days, \0"

PadTime:
	ld		a, c
	cp		2
	ret		z
	dec		hl
	ld		(hl), "0"
	inc		c
	ret
