.STAT:		equ CFBASE + 0x07	; Status (R)
.SRA:		equ DUARTBASE + 0x01	; Status Register A (R)
.RHRA:		equ DUARTBASE + 0x03	; Rx Holding Register A (R)

nullhandler:
	ei
	reti

diskhandler:
	push	af
	in		a, (.STAT)		; Read disk status
	pop	af
	ei
	reti

tickhandler:
	; Back up registers which we touch
	; The fewer the better!
	push	hl
	push	bc
	push	af
	; Check if UART buffering is on
	ld	a, (uart_buffering)
	cp	0xFF
	jr	nz, tickhandler_uptime
BufferConsumeLoop:
	; Check if the UART FIFO has any chars in it.
	in a, (.SRA)		; Read status reg
	and 00000001b		; Mask off receive ready
	xor 00000001b		; Compare receive ready to 1
	jr	nz, tickhandler_uptime
	; Do we have room in the buffer for those chars?
	ld	a, (uart_buffer_size)
	cp	16
	jr	z, tickhandler_uptime
	; Read a char from the UART FIFO
	in	a, (.RHRA)
	; Check for various special control characters
	cp	0x03		; Ctrl C
	jr	z, StackHandleCtrlC
	cp	0x18		; Ctrl X
	jr	z, StackHandleCtrlX
	cp	0x10		; Ctrl P
	jr	z, ToggleUARTPause
	; Copy UART char into buffer
	ld	hl, (uart_buffer_pointer)
	ld	(hl), a
	inc	hl
	ld	(uart_buffer_pointer), hl
	ld	hl, uart_buffer_size
	inc	(hl)
	jr	BufferConsumeLoop
ToggleUARTPause:
	ld	a, (uart_pause)
	cpl
	ld	(uart_pause), a
	jr	BufferConsumeLoop
StackHandleCtrlC:
	; This is a Ctrl-C, terminate current process!
	ld	hl, HandleCtrlC
	jr	StackHack
StackHandleCtrlX:
	; This is a Ctrl-X, enter debugger
	ld	hl, Monitor
StackHack:
	; Hack the return address
	pop	af
	pop	bc
	; At this point, the stack is pointing to our pushed
	; version of HL from when we entered tickhandler.
	; Below that in the stack is the return address into
	; the running program (if any).  Let's put the address
	; of the program exec/susp handler *above* that address,
	; then replace all the registers
	ex	(sp), hl
	push	hl
	push	bc
	push	af

tickhandler_uptime:
	;;;; Handle tick counter
	ld	hl, ticks
	inc	(hl)
	ld	a, (hl)
	cp	64
	jr	nz, tickhandler_alarm
	ld	(hl), 0

	; Update uptime seconds
	ld	hl, uptime
	ld	b, 8
UptimeLoop:
	inc	(hl)
	ld	a, (hl)
	cp	0
	jr	nz, tickhandler_alarm
	inc	hl
	djnz	UptimeLoop

tickhandler_alarm:
	ld	hl, sleep_countdown
	ld	a, (hl)
	jr	z, tickhandler_microsleep
	ld	a, (sleep_tick)
	ld	b, a
	ld	a, (ticks)
	cp	b
	jr	nz, tickhandler_microsleep
	dec	(hl)

tickhandler_microsleep:
	ld	hl, microsleep_countdown
	ld	a, (hl)
	jr	z, tickhandler_end
	dec	(hl)

tickhandler_end:
	in	a, (109)	; Reset RTC int
	pop	af
	pop	bc
	pop	hl		; Restore AF and HL
	ei			; Enable interrupts
	reti
